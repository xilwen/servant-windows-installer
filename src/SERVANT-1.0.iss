; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SERVANT"
#define MyAppVersion "1.0"
#define MyAppPublisher "SERVANT, Inc."
#define MyAppURL "http://www.servant.com/"
#define MyAppExeName "servant-manager-gui.exe"
#include <idp.iss>

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)

AppId={{B0A87C87-2BAD-4B17-B4A8-AE1DF88A991B}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
;
DisableWelcomePage=no
DisableReadyPage=yes
DisableDirPage=yes
DisableProgramGroupPage=yes
DisableReadyMemo=yes
;DisableReadyPage=yes
DisableStartupPrompt=yes
;
DefaultDirName={sd}\Users\{username}\{#MyAppName}
OutputDir=.\setup
OutputBaseFilename=SERVANTInstall
SetupIconFile=.\SERVANT.ico
Compression=lzma2/max
SolidCompression=yes
WizardImageFile=.\WalcomePage-SERVANT.bmp
WizardSmallImageFile=.\WizardSmallImageFile-1.bmp

[LangOptions]
DialogFontName=微軟正黑體
DialogFontSize=12
TitleFontName=Arial
TitleFontSize=36
WelcomeFontName=微軟正黑體
WelcomeFontSize=16
CopyrightFontName=微軟正黑體
CopyrightFontSize=12

[Messages]
; *** Application titles
SetupAppTitle=安裝
SetupWindowTitle=%1 安裝
UninstallAppTitle=解除安裝
UninstallAppFullTitle=解除安裝 %1

; *** Buttons
ButtonBack=上一步
ButtonNext=安裝
ButtonInstall=安裝
ButtonCancel=取消
ButtonFinish=完成

; *** "Welcome" wizard page
WelcomeLabel1=歡迎使用 [name] 安裝程式

; *** "Installing" wizard page
WizardInstalling=正在安裝
InstallingLabel=請稍候，安裝程式正在將 [name] 安裝到您的電腦上

; *** "Setup Completed" wizard page
FinishedHeadingLabel=安裝完成
FinishedLabel=安裝程式已經將 [name] 安裝在您的電腦中，您可以選擇程式的圖示來執行該應用程式。
ClickFinish=按 [完成] 以結束安裝程式。

; *** Uninstaller messages
ConfirmUninstall=您確定要完全移除 %1 及其相關的檔案嗎?
UninstallStatusLabel=正在從您的電腦移除 %1 中，請稍候...
UninstalledAll=%1 已經成功從您的電腦中移除。

; *** "Ready to Install" wizard page
WizardReady=準備安裝

[CustomMessages]
LaunchProgram=執行 %1

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: ".\servant-manager-gui\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: ".\vboxWrapper.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\SERVANT.ico"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{userprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\SERVANT.ico"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\SERVANT.ico"

[Run]
Filename: "{tmp}\VirtualBox-5.2.0-118431-Win.exe"; Flags: waituntilterminated; Check: IfNotVboxInstalled
Filename: "{app}\vboxWrapper.exe"; Parameters: " -install"; Flags: waituntilterminated runhidden
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallRun]
Filename: "{app}\vboxWrapper.exe"; Parameters: " -remove"; Flags: runhidden

[Code]
function IfNotVboxInstalled: Boolean;
begin
  if IsWin64() then begin
    Result := RegKeyExists(HKLM64, 'SOFTWARE\Oracle\VirtualBox');
    if not Result then begin
      Result := RegKeyExists(HKLM32, 'SOFTWARE\\Oracle\\VirtualBox');
    end;
  end else begin
    Result := RegKeyExists(HKLM32, 'SOFTWARE\\Oracle\\VirtualBox');
  end;
  Result := not Result;
end;

procedure InitializeWizard;
var
  RichViewer: TRichEditViewer;
begin
  RichViewer := TRichEditViewer.Create(WizardForm);
  RichViewer.Left := WizardForm.WelcomeLabel2.Left;
  RichViewer.Top := WizardForm.WelcomeLabel2.Top;
  RichViewer.Width := WizardForm.WelcomeLabel2.Width;
  RichViewer.Height := WizardForm.WelcomeLabel2.Height;
  RichViewer.Parent := WizardForm.WelcomeLabel2.Parent;
  RichViewer.BorderStyle := bsNone;
  RichViewer.TabStop := False;
  RichViewer.ReadOnly := True;
  WizardForm.WelcomeLabel2.Visible := False;

  RichViewer.RTFText :=
    '{\rtf1 將會在您的使用者家目錄下安裝 SERVANT 。' + 
    '\par SERVANT 需要使用 Virtualbox 。' +
    '\par 若電腦中沒有安裝 Oracle Virtualbox 5.0 以上版本，安裝時將會下載並執行 Oracle Virtualbox 安裝程式。' +
    '\par \par 繼續安裝代表您同意 ' +
    '{\b {\field{\*\fldinst{HYPERLINK "https://github.com/xilwen/servant-manager-gui/blob/master/LICENSE" }}' + 
    '{\fldrslt{授權合約}}}} ' +
    '。}';

  if IfNotVboxInstalled then begin
    idpAddFile('http://download.virtualbox.org/virtualbox/5.2.0/VirtualBox-5.2.0-118431-Win.exe', ExpandConstant('{tmp}\VirtualBox-5.2.0-118431-Win.exe'));
    idpDownloadAfter(wpWelcome);
  end;
end;


